---
title: BCP139
description: Error - A resource's scope must match the scope of the Bicep file for it to be deployable. You must use modules to deploy resources to a different scope.
ms.topic: reference
ms.custom: devx-track-bicep
ms.date: 09/19/2024
---

# Bicep error code - BCP139

This error occurs when you use [`resource`](../file.md#resources) to deploy resources to a different scope than the target one. You should use [`module`](../file.md#modules) instead. For more information, see the following articles based on the scope:

- Resource group: [Scope to different resource group](../deploy-to-resource-group.md#scope-to-different-resource-group).
- Subscription: [Deployment scopes](../deploy-to-subscription.md#deployment-scopes).
- Management group: [Deployment scopes](../deploy-to-management-group.md#deployment-scopes).
- Tenant: [Deployment scopes](../deploy-to-tenant.md#deployment-scopes).

## Error description

`A resource's scope must match the scope of the Bicep file for it to be deployable. You must use modules to deploy resources to a different scope.`

## Solution

To deploy resources to a scope that isn't the target scope, add a `module`.

## Examples

The following example deploys a storage account resource to a different resource group in the same subscription. The example raises the error because the `module` declaration type isn't used:

```bicep
param otherResourceGroup string
param location string 

resource storage 'Microsoft.Storage/storageAccounts@2023-05-01' = {
  name: uniqueString(resourceGroup().id)
  scope: resourceGroup(otherResourceGroup)
  location: location
  sku: {
    name: 'Standard_LRS'
  }
  kind: 'StorageV2'
}
```

You can fix the error by using the `module` declaration type:

```bicep
param otherResourceGroup string

// module deployed to resource group in the same subscription
module exampleModule 'module.bicep' = {
  name: 'deployStorageToAnotherRG'
  scope: resourceGroup(otherResourceGroup)
}
```

The following example deploys a resource group to a different subscription. The example raises the error because `module` isn't used

```bicep
targetScope = 'subscription'

param otherSubscriptionID string

// module deployed at subscription level but in a different subscription
resource exampleResource 'Microsoft.Resources/resourceGroups@2024-03-01' =  {
  name: 'deployToDifferentSub'
  scope: subscription(otherSubscriptionID)
  location: 'eastus'
}
```

You can fix the error by using the `module` declaration type:

```bicep
targetScope = 'subscription'

param otherSubscriptionID string

// module deployed at subscription level but in a different subscription
module exampleModule 'module.bicep' = {
  name: 'deployToDifferentSub'
  scope: subscription(otherSubscriptionID)
}
```

## Next steps

For more information about Bicep error and warning codes, see [Bicep core diagnostics](../bicep-core-diagnostics.md).
