---
title: BCP139
description: A resource's scope must match the scope of the Bicep file for it to be deployable. You must use modules to deploy resources to a different scope.
ms.topic: reference
ms.custom: devx-track-bicep
ms.date: 06/25/2025
---

# Bicep diagnostic code - BCP139

This diagnostic occurs when you use [`resource`](../file.md#resources) to deploy resources to a different scope than the target one. You should use [`module`](../file.md#modules) instead. For more information, see the following articles based on the scope:

- Resource group: [Scope to different resource group](../deploy-to-resource-group.md#scope-to-different-resource-group).
- Subscription: [Deployment scopes](../deploy-to-subscription.md#deployment-scopes).
- Management group: [Deployment scopes](../deploy-to-management-group.md#deployment-scopes).
- Tenant: [Deployment scopes](../deploy-to-tenant.md#deployment-scopes).

## Description

A resource's scope must match the scope of the Bicep file for it to be deployable. You must use modules to deploy resources to a different scope.

## Level

Error

## Solution

To deploy resources to a scope that isn't the target scope, add a `module`.

## Examples

The following example deploys a storage account resource to a different resource group in the same subscription. The example raises the diagnostic because the `module` declaration type isn't used:

```bicep
param targetResourceGroup string
param storageAccountName string 
param location string 

// resource deployed to a different resource group in the same subscription
resource storage 'Microsoft.Storage/storageAccounts@2023-05-01' = {
  name: storageAccountName
  scope: resourceGroup(targetResourceGroup)
  location: location
  sku: {
    name: 'Standard_LRS'
  }
  kind: 'StorageV2'
}
```

You can fix the diagnostic by using the `module` declaration type:

```bicep
param targetResourceGroup string 
param storageAccountName string = uniqueString(resourceGroup().id)
param location string = 'eastus'

module storageModule './module.bicep' = {
  name: 'deployStorage'
  scope: resourceGroup(targetResourceGroup)
  params: {
    storageAccountName: storageAccountName
    location: location
  }
}
```

The following example deploys a resource group to a different subscription. The example raises the diagnostic because `module` isn't used

```bicep
targetScope = 'subscription'

param targetSubscriptionID string
param resourceGroupName string
param location string 

// resource deployed to a different subscription
resource exampleResource 'Microsoft.Resources/resourceGroups@2024-03-01' =  {
  name: resourceGroupName
  scope: subscription(targetSubscriptionID)
  location: location
}
```

You can fix the diagnostic by using the `module` declaration type:

```bicep
targetScope = 'subscription'

param otherSubscriptionID string
param resourceGroupName string
param location string

// module deployed to a different subscription
module exampleModule 'module.bicep' = {
  name: 'deployToDifferentSub'
  scope: subscription(otherSubscriptionID)
  params: {
    resourceGroupName: resourceGroupName
    location: location
  }
}
```

## Next steps

For more information about Bicep diagnostics, see [Bicep core diagnostics](../bicep-core-diagnostics.md).
